/*
BIN: make binary
UNARY: make the second last node the parent of the last node
PUSH: marks the previous node and adds it to the node stack
NOP: pushes an epsilon node
NEW_X: pushes a new node with name X.
CHK: verifies if the top node has children. If no children, makes label epsilon
COND_UNARY: verifies if the top node is epsilon, if so pops it. else unary
IUNARY: make the last node the parent of the seocnd last node
CHK_MERGE: merges the child of the node with the parent if the parent has the child as the only one.
FLAT: flattens the children of the current node to the parent node
*/

Start -> Prog .
Prog -> NEW_Prog NEW_ClassList ClassList CHK COND_UNARY NEW_FuncDefList FuncDefList CHK COND_UNARY main PUSH FuncBody UNARY .

ClassList -> ClassDecl UNARY ClassList
	| .
ClassDecl -> class PUSH id PUSH UNARY InheritList lcurbr MembList rcurbr semi .

InheritList -> inherits PUSH id PUSH UNARY InheritList2 UNARY
    | .

InheritList2 -> comma id PUSH UNARY InheritList2
	| .

MembList -> NEW_declaration DataMember UNARY MembList
	| .
DataMember -> Visibility UNARY MemberDecl UNARY.
MemberDecl -> FuncDecl
	| VarDecl semi .
FuncDecl -> func PUSH id PUSH UNARY lpar NEW_parameters FParamList CHK COND_UNARY rpar colon FuncType UNARY SHIFT semi  .
FParamList -> VarDecl UNARY FParamList2
	| .
FParamList2 -> comma VarDecl UNARY FParamList2
	| .


DimList -> lsqbr Intnum UNARY rsqbr DimList
	| .
VarDecl -> NEW_parameter Type UNARY id PUSH UNARY NEW_DimList DimList CHK COND_UNARY .





FuncDefList -> FuncDef UNARY FuncDefList
	| .

FuncDef -> func PUSH id PUSH UNARY ScopeSpec UNARY lpar NEW_args FParamList CHK COND_UNARY rpar colon FuncType UNARY SHIFT NEW_body FuncBody UNARY .

FuncBody -> lcurbr LocalDecls NEW_body StatementList CHK COND_UNARY rcurbr .

LocalDecls -> var PUSH lcurbr VarDeclList rcurbr UNARY
    | .

VarDeclList -> VarDecl UNARY semi VarDeclList
	| .
StatementList -> Statement UNARY StatementList
	| .
Statement -> id PUSH VarFunc semi
	| if PUSH lpar Expr UNARY rpar then StatBlock UNARY else StatBlock UNARY semi
	| while PUSH lpar Expr UNARY rpar StatBlock UNARY semi
	| read PUSH lpar VarList UNARY rpar semi
	| write PUSH lpar Expr UNARY rpar semi
	| return PUSH lpar Expr UNARY rpar semi
	| break PUSH semi
	| continue PUSH semi  .

VarFunc -> Var VarFunc1 VarFunc3 | FCall VarFunc1 .
VarFunc1 -> dot PUSH id PUSH VarFunc2 BIN | .
VarFunc2 -> Var VarFunc1
    | FCall VarFunc1 .
VarFunc3 -> assign PUSH Expr BIN | .

Var -> NEW_variable IUNARY NEW_index IndiceList CHK COND_UNARY .
IndiceList -> lsqbr Expr UNARY rsqbr IndiceList
	| .

FCall -> lpar NEW_Call IUNARY NEW_parameters AParams CHK COND_UNARY rpar .
AParams -> Expr UNARY AParams2
	| .
AParams2 -> comma Expr UNARY AParams2
	| .
Expr -> ArithExpr Expr2 .
Expr2 -> RelOp PUSH ArithExpr BIN
	| .
ArithExpr -> Term ArithExpr2 .
ArithExpr2 -> AddOp PUSH Term BIN ArithExpr2
	| .
Term -> Factor Term2 .
Term2 -> MultOp PUSH Factor BIN Term2
	| .
Factor -> FuncVarList
	| intnum PUSH
	| floatnum PUSH
	| stringlit PUSH
	| lpar Expr rpar
	| not PUSH Factor UNARY
	| Sign PUSH Factor UNARY
	| qm lsqbr NEW_Factor Expr UNARY colon Expr UNARY colon Expr UNARY rsqbr .
FuncVarList -> id PUSH FuncVarList2 FuncVarList3 .
FuncVarList2 -> Var
    | FCall .
FuncVarList3 -> dot PUSH id PUSH FuncVarList2 BIN
    | .
StatBlock -> lcurbr NEW_Block StatementList rcurbr
	| NEW_StatBlock Statement UNARY
	| NOP .
VarList -> id PUSH VarList2 .
VarList2 -> Var VarList4
    | FCall VarList3 .
VarList3 -> dot PUSH id PUSH VarList2 BIN .
VarList4 -> VarList3
    | .


ScopeSpec -> sr id PUSH
	| NOP .
RelOp -> eq
	| neq
	| lt
	| gt
	| leq
	| geq .
Sign -> plus
	| minus .
Type -> integer PUSH
	| float PUSH
	| string PUSH
	| id PUSH .
Intnum -> intnum PUSH
	| NOP .
MultOp -> mult
	| div
	| and .
Visibility -> public PUSH
	| private PUSH
	| NEW_protected
	| protected PUSH .

FuncType -> Type
	| void PUSH .
AddOp -> plus
	| minus
	| or .

BIN -> .
UNARY -> .
PUSH -> .
NOP -> .
CHK -> .


NEW_ClassList -> .

NEW_DimList -> .
NEW_Factor -> .
NEW_Call -> .


NEW_Prog -> .
NEW_StatBlock -> .
NEW_variable -> .

NEW_FuncDefList -> .


NEW_protected -> .
NEW_Block -> .
COND_UNARY -> .
IUNARY -> .

NEW_body -> .
NEW_parameter -> .
NEW_declaration -> .
NEW_index -> .
NEW_args -> .
NEW_parameters -> .
SHIFT -> .
