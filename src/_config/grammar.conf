/*
BIN: make binary
UNARY: make the second last node the parent of the last node
PUSH: marks the previous node and adds it to the node stack
NOP: pushes an epsilon node
NEW_X: pushes a new node with name X.
CHK: verifies if the top node has children. If no children, makes label epsilon
*/

Start -> Prog .
Prog -> NEW_Prog ClassList UNARY FuncDefList UNARY main FuncBody UNARY .

ClassList -> NEW_ClassList ClassList2 .
ClassList2 -> ClassDecl UNARY ClassList2
	| .
ClassDecl -> class PUSH id PUSH UNARY InheritList UNARY lcurbr MembList UNARY rcurbr semi .
InheritList -> inherits PUSH id PUSH UNARY InheritList2
	| NOP .
InheritList2 -> comma id PUSH UNARY InheritList2
	| .
MembList -> NEW_MembList MembList2 CHK .
MembList2 -> DataMember UNARY MembList2
	| .
DataMember -> NEW_DataMember Visibility UNARY MemberDecl UNARY .
MemberDecl -> FuncDecl
	| VarDecl .
FuncDecl -> func PUSH id PUSH UNARY lpar FParamList UNARY rpar colon FuncType UNARY semi  .
FParamList -> NEW_FParamList FParam UNARY FParamList2
	| NOP .
FParamList2 -> comma FParam UNARY FParamList2
	| .
FParam -> NEW_fParam Type UNARY id PUSH UNARY DimList UNARY .
DimList -> NEW_DimList DimList2 CHK .
DimList2 -> lsqbr Intnum UNARY rsqbr DimList2
	| .
VarDecl -> NEW_VarDecl Type UNARY id PUSH UNARY DimList UNARY semi .

FuncDefList -> NEW_FuncDefList FuncDefList2 .
FuncDefList2 -> FuncDef UNARY FuncDefList2
	| .
FuncDef -> func PUSH id PUSH UNARY ScopeSpec UNARY lpar FParamList UNARY rpar colon FuncType UNARY FuncBody UNARY .
FuncBody -> NEW_FuncBody lcurbr FuncBody2 UNARY StatementList UNARY rcurbr  .
FuncBody2 -> var PUSH lcurbr VarDeclList UNARY rcurbr
	| NOP .
StatementList -> NEW_StatementList StatementList2 .
StatementList2 -> Statement UNARY StatementList2
	| .
Statement -> Statement1
	| if PUSH lpar Expr UNARY rpar then StatBlock UNARY else StatBlock UNARY semi
	| while PUSH lpar Expr UNARY rpar StatBlock UNARY semi
	| read PUSH lpar VarList UNARY rpar semi
	| write PUSH lpar Expr UNARY rpar semi
	| return PUSH lpar Expr UNARY rpar semi
	| break PUSH semi
	| continue PUSH semi  .

Statement1 -> id PUSH Statement8 semi .
Statement9 -> assign PUSH Expr BIN | .
Statement8 -> Var Statement3 Statement9 | FCall Statement4 .
Statement2 -> Var Statement3
    | FCall Statement4 .
Statement3 -> dot PUSH id PUSH Statement2 BIN | .
Statement4 ->  dot PUSH id PUSH Statement2 BIN | .
Var -> NEW_variable IndiceList BIN .
IndiceList -> NEW_IndiceList IndiceList2 CHK .
IndiceList2 -> lsqbr Expr UNARY rsqbr IndiceList2
	| .
FCall -> lpar NEW_FCall AParams rpar BIN .
AParams -> NEW_aParams Expr UNARY AParams2
	| NOP .
AParams2 -> comma Expr UNARY AParams2
	| .
Expr -> ArithExpr Expr2 .
Expr2 -> RelOp PUSH ArithExpr BIN
	| .
ArithExpr -> Term ArithExpr2 .
ArithExpr2 -> AddOp PUSH Term BIN ArithExpr2
	| .
Term -> Factor Term2 .
Term2 -> MultOp PUSH Factor BIN Term2
	| .
Factor -> FuncVarList
	| intnum PUSH
	| floatnum PUSH
	| stringlit PUSH
	| lpar Expr rpar
	| not PUSH Factor UNARY
	| Sign PUSH Factor UNARY
	| qm lsqbr NEW_Factor Expr UNARY colon Expr UNARY colon Expr UNARY rsqbr .
FuncVarList -> id PUSH FuncVarList2 FuncVarList3 .
FuncVarList2 -> Var
    | FCall .
FuncVarList3 -> dot PUSH id PUSH FuncVarList2 BIN
    | .
StatBlock -> lcurbr NEW_StatBlock StatementList UNARY rcurbr
	| NEW_StatBlock Statement UNARY
	| NOP .
VarList -> id PUSH VarList2 .
VarList2 -> Var VarList4
    | FCall VarList3 .
VarList3 -> dot PUSH id PUSH VarList2 BIN .
VarList4 -> VarList3
    | .
VarDeclList -> NEW_VarDeclList VarDeclList2 .
VarDeclList2 -> VarDecl UNARY VarDeclList2
	| .







ScopeSpec -> sr id PUSH
	| NOP .
RelOp -> eq
	| neq
	| lt
	| gt
	| leq
	| geq .
Sign -> plus
	| minus .
Type -> integer PUSH
	| float PUSH
	| string PUSH
	| id PUSH .
Intnum -> intnum PUSH
	| NOP .
MultOp -> mult
	| div
	| and .
Visibility -> public PUSH
	| private PUSH
	| NOP .

FuncType -> Type
	| void PUSH .
AddOp -> plus
	| minus
	| or .

BIN -> .
UNARY -> .
PUSH -> .
NOP -> .
CHK -> .

NEW_aParams -> .
NEW_ClassList -> .
NEW_DataMember -> .
NEW_DimList -> .
NEW_Factor -> .
NEW_FCall -> .
NEW_fParam -> .
NEW_FParamList -> .
NEW_IndiceList -> .
NEW_MembList -> .
NEW_Prog -> .
NEW_StatBlock -> .
NEW_StatementList -> .
NEW_variable -> .
NEW_VarDecl -> .
NEW_FuncDefList -> .
NEW_VarDeclList -> .
NEW_FuncBody -> .