/*
BIN: make binary
UNARY: make the second last node the parent of the last node
IUNARY: reverse UNARY op. Make the last node the parent of the second last node
PUSH: marks the previous node and adds it to the node stack
NOP: pushes an epsilon node
NULL: makes the last node an epsilon node
POP: pops the last node
NEW_X: pushes a new node with name X.
CHK: verifies if the top node has children. If no children, makes label epsilon
*/

Start -> NEW_Prog Prog UNARY .
Prog -> NEW_ClassList ClassList CHK UNARY NEW_FuncDefList FuncDefList CHK UNARY main PUSH FuncBody UNARY .

ClassList -> ClassDecl UNARY ClassList
	| .
ClassDecl -> class PUSH id PUSH UNARY InheritList UNARY lcurbr NEW_MembList MembList CHK UNARY rcurbr semi .
InheritList -> inherits PUSH id PUSH UNARY InheritList2
	| NOP .
InheritList2 -> comma id PUSH UNARY InheritList2
	| .
MembList -> DataMember UNARY MembList
	| .
DataMember -> Visibility NEW_DataMember PUSH UNARY MemberDecl UNARY .
MemberDecl -> FuncDecl
	| VarDecl .
FuncDecl -> func PUSH id PUSH UNARY lpar FParamList UNARY rpar colon FuncType PUSH UNARY semi  .
FParamList -> NEW_FParamList FParam UNARY FParamList2
	| NOP .
FParamList2 -> comma FParam UNARY FParamList2
	| .
FParam -> NEW_fParam Type PUSH UNARY id PUSH UNARY NEW_DimList DimList CHK UNARY .
DimList -> lsqbr Intnum UNARY rsqbr DimList
	| .
VarDecl -> NEW_VarDecl Type PUSH UNARY id PUSH UNARY NEW_DimList DimList CHK UNARY semi .


FuncDefList -> FuncDef UNARY FuncDefList
	| .
FuncDef -> func PUSH id PUSH UNARY ScopeSpec UNARY lpar FParamList UNARY rpar colon FuncType PUSH UNARY NEW_FuncBody FuncBody CHK UNARY .
FuncBody -> lcurbr FuncBody2 UNARY NEW_StatementList StatementList CHK UNARY rcurbr  .
FuncBody2 -> var PUSH lcurbr NEW_VarDeclList VarDeclList CHK UNARY rcurbr
	| NOP .
StatementList -> Statement UNARY StatementList
	| .
Statement -> id PUSH Statement2
	| if PUSH lpar Expr UNARY rpar then StatBlock UNARY else StatBlock UNARY semi
	| while PUSH lpar Expr UNARY rpar StatBlock UNARY semi
	| read PUSH lpar VarList UNARY rpar semi
	| write PUSH lpar Expr UNARY rpar semi
	| return PUSH lpar Expr UNARY rpar semi
	| break PUSH semi
	| continue PUSH semi  .
Statement2 -> Var Statement3
    | FCall Statement4 .
Statement3 -> dot PUSH id PUSH BIN Statement2 | assign PUSH Expr BIN semi .
Statement4 ->  dot PUSH id PUSH BIN Statement2 | semi .
Var -> NEW_Var NEW_IndiceList IndiceList BIN .
IndiceList -> lsqbr Expr UNARY rsqbr IndiceList
	| .
FCall -> lpar NEW_FCall AParams rpar BIN .
AParams -> Expr NEW_AParams IUNARY AParams2
	| NOP .
AParams2 -> comma Expr UNARY AParams2
	| .
Expr -> ArithExpr Expr2 .
Expr2 -> RelOp PUSH ArithExpr BIN
	| .
ArithExpr -> Term ArithExpr2 .
ArithExpr2 -> AddOp PUSH Term BIN ArithExpr2
	| .
Term -> Factor Term2 .
Term2 -> MultOp PUSH Factor BIN Term2
	| .
Factor -> FuncVarList
	| intnum PUSH
	| floatnum PUSH
	| stringlit PUSH
	| lpar Expr rpar
	| not PUSH Factor UNARY
	| Sign PUSH Factor UNARY
	| qm lsqbr NEW_Factor Expr UNARY colon Expr UNARY colon Expr UNARY rsqbr .
FuncVarList -> id PUSH FuncVarList2 .
FuncVarList2 -> Var FuncVarList3
    | FCall FuncVarList3 .
FuncVarList3 -> dot PUSH id PUSH BIN FuncVarList2
    | .
StatBlock -> lcurbr NEW_StatBlock StatementList CHK rcurbr
	| NEW_StatBlock Statement UNARY
	| NOP .
VarList -> id PUSH VarList2 .
VarList2 -> Var VarList4 | FCall VarList3 .
VarList3 ->  dot PUSH id PUSH BIN VarList2 .
VarList4 -> VarList3 | .
VarDeclList -> VarDecl UNARY VarDeclList
	| .







ScopeSpec -> sr id PUSH
	| NOP .
RelOp -> eq
	| neq
	| lt
	| gt
	| leq
	| geq .
Sign -> plus
	| minus .
Type -> integer
	| float
	| string
	| id .
Intnum -> intnum PUSH
	| NOP .
MultOp -> mult
	| div
	| and .
Visibility -> public
	| private
	| .
FuncType -> Type
	| void .
AddOp -> plus
	| minus
	| or .

BIN -> .
UNARY -> .
IUNARY -> .
PUSH -> .

NOP -> .
NEW_AParams -> .
NEW_ClassList -> .
NEW_DataMember -> .
NEW_DimList -> .
NEW_Factor -> .
NEW_FCall -> .
NEW_fParam -> .
NEW_FParamList -> .
NEW_IndiceList -> .
NEW_MembList -> .
NEW_Prog -> .
NEW_StatBlock -> .
NEW_StatementList -> .
NEW_Var -> .
NEW_VarDecl -> .
CHK -> .
NEW_FuncDefList -> .
NEW_VarDeclList -> .
NEW_FuncBody -> .