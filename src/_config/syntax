Start -> Prog .


Prog -> ClassList FuncDefList main FuncBody .

ClassList -> ClassDecl ClassList
	| .
ClassDecl -> class id InheritList lcurbr MembList rcurbr semi .

InheritList -> inherits id InheritList2
	| .
InheritList2 -> comma id InheritList2
	| .

MemberDecl -> FuncDecl
	| VarDecl .
DataMember -> Visibility MemberDecl .
MembList -> DataMember MembList
	| .

AParams -> Expr AParams2
	| .
AParams2 -> comma Expr AParams2
	| .


ArithExpr -> Term ArithExpr2 .
ArithExpr2 -> AddOp Term ArithExpr2
	| .


Expr -> ArithExpr Expr2 .
Expr2 -> RelOp ArithExpr
	| .


Term -> Factor Term2 .
Term2 -> MultOp Factor Term2
	| .


IndiceList -> lsqbr Expr rsqbr IndiceList
	| .



FuncVarList -> id FuncVarList2 .
FuncVarList2 -> Var ADOPT FuncVarList3
    | FCall ADOPT FuncVarList3 .
FuncVarList3 -> dot FuncVarList
    | .
FCall -> lpar AParams rpar .
Var -> IndiceList .
Factor -> FuncVarList
	| intnum
	| floatnum
	| stringlit
	| lpar Expr rpar
	| not Factor
	| Sign Factor
	| qm lsqbr Expr colon Expr colon Expr rsqbr .


DimList -> lsqbr Intnum rsqbr DimList
	| .


VarDecl -> Type id DimList semi .
VarDeclList -> VarDecl VarDeclList
	| .


FParam -> Type id DimList .
FParamList -> FParam FParamList2
	| .
FParamList2 -> comma FParam FParamList2
	| .


FuncDefList -> FuncDef FuncDefList
	| .
FuncDef -> func id ScopeSpec lpar FParamList rpar colon FuncType FuncBody .


FuncBody -> lcurbr FuncBody2 StatementList rcurbr  .
FuncBody2 -> var lcurbr VarDeclList rcurbr
	| .


FuncDecl -> func id lpar FParamList rpar colon FuncType semi  .


StatBlock -> lcurbr StatementList rcurbr
	| Statement
	| .


StatementList -> Statement StatementList
	| .


Statement -> FuncAssignList semi
	| if lpar Expr rpar then StatBlock else StatBlock semi
	| while lpar Expr rpar StatBlock semi
	| read lpar VarList rpar semi
	| write lpar Expr rpar semi
	| return lpar Expr rpar semi
	| break semi
	| continue semi  .

FuncAssignList -> FuncAssignList1
    | FuncAssignList2 .
FuncAssignList1 -> IndiceList #Var FuncAssignList3 .
FuncAssignList2 -> lpar AParams rpar #FCall FuncAssignList4 .
FuncAssignList3 -> dot id FuncAssignList | assign Expr .
FuncAssignList4 ->  dot id FuncAssignList | .




FuncOrAssignList -> id FuncOrAssignList2 .
FuncOrAssignList2 -> IndiceList FuncOrAssignList3
	| lpar AParams rpar FuncOrAssignList4 .
FuncOrAssignList3 -> dot id FuncOrAssignList2
	| assign Expr .
FuncOrAssignList4 -> dot id FuncOrAssignList5
	| .
FuncOrAssignList5 -> IndiceList dot id FuncOrAssignList5
	| lpar AParams rpar FuncOrAssignList6 .
FuncOrAssignList6 -> dot id FuncOrAssignList5
	| .


VarList -> id VarList2 .
VarList2 -> IndiceList VarList3 .
VarList3 -> dot id VarList2
	| .


ScopeSpec -> sr id
	| .
RelOp -> eq
	| neq
	| lt
	| gt
	| leq
	| geq .
Sign -> plus
	| minus .
Type -> integer
	| float
	| string
	| id .
Intnum -> intnum
	| .
MultOp -> mult
	| div
	| and .
Visibility -> public
	| private
	| .
FuncType -> Type
	| void .
AddOp -> plus
	| minus
	| or .
