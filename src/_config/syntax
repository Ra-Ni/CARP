Start -> Prog .


Prog -> ClassList FuncDefList main FuncBody .

ClassList -> ClassDecl ClassList
	| .
ClassDecl -> class id InheritList lcurbr MembList rcurbr semi .

InheritList -> inherits id InheritList2
	| .
InheritList2 -> comma id InheritList2
	| .

MemberDecl -> FuncDecl
	| VarDecl .
DataMember -> Visibility MemberDecl .
MembList -> DataMember MembList
	| .

AParams -> Expr AParams2
	| .
AParams2 -> comma Expr AParams2
	| .


ArithExpr -> Term ArithExpr2 .
ArithExpr2 -> AddOp Term ArithExpr2
	| .


Expr -> ArithExpr Expr2 .
Expr2 -> RelOp ArithExpr
	| .


Term -> Factor Term2 .
Term2 -> MultOp Factor Term2
	| .


IndiceList -> lsqbr Expr rsqbr IndiceList
	| .


ADOPT -> .
FuncVarList -> id FuncVarList2 .
FuncVarList2 -> Var ADOPT FuncVarList3
    | FCall ADOPT FuncVarList3 .
FuncVarList3 -> dot FuncVarList
    | .
FCall -> lpar AParams rpar .
Var -> IndiceList .
Factor -> FuncVarList
	| intnum
	| floatnum
	| stringlit
	| lpar Expr rpar
	| not Factor
	| Sign Factor
	| qm lsqbr Expr colon Expr colon Expr rsqbr .


DimList -> lsqbr Intnum rsqbr DimList
	| .


VarDecl -> Type id DimList semi .
VarDeclList -> VarDecl VarDeclList
	| .


FParam -> Type id DimList .
FParamList -> FParam FParamList2
	| .
FParamList2 -> comma FParam FParamList2
	| .


FuncDefList -> FuncDef FuncDefList
	| .
FuncDef -> func id ScopeSpec lpar FParamList rpar colon FuncType FuncBody .


FuncBody -> lcurbr FuncBody2 StatementList rcurbr  .
FuncBody2 -> var lcurbr VarDeclList rcurbr
	| .


FuncDecl -> func id lpar FParamList rpar colon FuncType semi  .


StatBlock -> lcurbr StatementList rcurbr
	| Statement
	| .


StatementList -> Statement StatementList
	| .


Statement -> id Statement2
	| if lpar Expr rpar then StatBlock else StatBlock semi
	| while lpar Expr rpar StatBlock semi
	| read lpar VarList rpar semi
	| write lpar Expr rpar semi
	| return lpar Expr rpar semi
	| break semi
	| continue semi  .



Statement2 -> Statement3
    | Statement4 .
Statement3 -> Var ADOPT Statement5 .
Statement4 -> FCall ADOPT Statement6 .
Statement5 -> dot id Statement2 | assign Expr semi .
Statement6 ->  dot id Statement2 | semi .

VarList -> id VarList2 .
VarList2 -> IndiceList VarList3 .
VarList3 -> dot id VarList2
	| .


ScopeSpec -> sr id
	| .
RelOp -> eq
	| neq
	| lt
	| gt
	| leq
	| geq .
Sign -> plus
	| minus .
Type -> integer
	| float
	| string
	| id .
Intnum -> intnum
	| .
MultOp -> mult
	| div
	| and .
Visibility -> public
	| private
	| .
FuncType -> Type
	| void .
AddOp -> plus
	| minus
	| or .
