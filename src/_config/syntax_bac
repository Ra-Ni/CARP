START -> Prog .
Prog -> ClassDecl FuncDef main FuncBody.
ClassDecl -> class id Inherit opencubr ClassDeclBody closecubr semi ClassDecl
	| .
FuncDef -> Function FuncDef 
	| .
Function -> FuncHead FuncBody.
Inherit -> inherits id NestedId 
	| .
NestedId -> comma id NestedId 
	| .
Visibility -> public 
	| private 
	| .
MemberDecl -> FuncDecl 
	| VarDecl .
ClassDeclBody -> Visibility MemberDecl ClassDeclBody 
	| .
FuncDecl -> func id openpar FParams closepar colon FuncDeclTail semi.
FuncDeclTail -> Type 
	| void .
VarDeclRep	-> VarDecl VarDeclRep 
	| .
VarDecl -> Type id ArraysizeTail semi .
FParams -> Type id ArraysizeTail FParamsTail 
	| .
Type -> integer 
	| float 
	| string 
	| id .
FParamsTail -> comma Type id ArraysizeTail FParamsTail
	| .
Arraysize -> opensqbr IntNum closesqbr .
ArraysizeTail -> Arraysize ArraysizeTail 
	| .
IntNum -> intnum 
	| .
FuncHead -> func id ClassMethod openpar FParams closepar colon FuncDeclTail.
FuncBody -> opencubr MethodBody StatementTail closecubr.
ClassMethod -> coloncolon id 
	| .
MethodBody -> var opencubr VarDeclRep closecubr 
	| .
StatementTail -> Statement StatementTail 
	| .
Statement -> FuncVarStartCaveat semi 
	| if openpar ArithExpr closepar then StatBlock else StatBlock semi 
 	| while openpar ArithExpr closepar StatBlock semi 
 	| read openpar IdnestStart closepar semi 
 	| write openpar Expr closepar semi 
 	| return openpar Expr closepar semi 
 	| break semi 
 	| continue semi .
AssignStat ->  AssignOp Expr.
AssignOp -> assign .
Expr -> ArithExpr ExprTail.
ArithExpr -> Term ArithExprTail .
ArithExprTail -> AddOp Term ArithExprTail
    | .
ExprTail -> RelOp ArithExpr | .
StatBlock -> opencubr StatementTail closecubr
	| Statement
	| .
RelOp -> eq 
	| noteq 
	| lt 
	| gt 
	| leq 
	| geq .
AddOp -> plus 
	| minus 
	| or.
MultOp -> mult 
	| div 
	| and .
Term -> Factor TermTail .
TermTail -> MultOp Factor TermTail 
	| .
Factor -> FuncVarStart 
	| intnum 
	| floatnum 
	| stringlit 
	| openpar ArithExpr closepar 
	| not Factor 
	| Sign Factor 
	| qmark opensqbr Expr colon Expr colon Expr closesqbr .
Sign -> plus 
	| minus .
AParams -> Expr AParamsTail 
	| .
AParamsTail -> comma Expr AParamsTail 
	| .
IdnestStart -> id IdnestVarBody .
IdnestVarBody -> IndiceRep IdnestVarBodyTail 
	| openpar AParams closepar dot id IdnestVarBody .
IdnestVarBodyTail -> dot id IdnestVarBody 
	| .
FuncVarStart -> id IdnestBody.
IdnestBody -> IndiceRep IdnestBodyTail
	| openpar AParams closepar IdnestBodyTail .
IdnestBodyTail -> dot id IdnestBody 
	| .
FuncVarStartCaveat -> id IdnestCaveatBody.
IdnestCaveatBody -> IndiceRep IdnestCaveatBodyTail
	| openpar AParams closepar IdnestCaveatBodyTailTwo .
IdnestCaveatBodyTail -> dot id IdnestCaveatBody 
	| AssignStat .
IdnestCaveatBodyTailTwo -> dot id IdnestCaveatBody 
	| .
IndiceRep -> opensqbr ArithExpr closesqbr IndiceRep 
	| .